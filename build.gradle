apply plugin: 'java'
apply plugin: "groovy"
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.6
version = '1.0.2'
group = "com.oohlalog"

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}




configurations {
    deployerJars
}

jar {
    manifest {
        attributes 'Implementation-Title': 'oohlalog-4j', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-http:2.2"
}

dependencies {
    compile 'log4j:log4j:1.2.17'
	compile 'com.google.code.gson:gson:2.2.4'
	testCompile("org.codehaus.groovy:groovy-all:1.8.9")
    testCompile 'org.scala-lang:scala-library:2.11.0'
    testCompile 'com.outr.javasysmon:javasysmon_2.10:0.3.4'
	testCompile "org.spockframework:spock-core:0.7-groovy-1.8"
}

test {
    outputs.upToDateWhen { false }
    systemProperties 'property': 'value'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'oohlalog-4j'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'oohlalog-4j'
                    description 'Provides a log4j appender to send logs to the cloud via the oohlalog cloud logging service.'
                    url 'http://www.oohlalog.com'
                    scm {
                        url 'https://github.com/oohlalog/oohlalog_log4j'
                        connection 'scm:https://bertramdev@github.com/oohlalog/oohlalog_log4j.git'
                        developerConnection 'scm:git://github.com/oohlalog/oohlalog_log4j.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'destes@bcap.com'
                        }
                        developer {
                            id 'jlenf'
                            name 'Jeremy Leng'
                            email 'jleng@bcap.com'
                        }
                        developer {
                            id 'jsaardchit'
                            name 'Jordon Saardchit'
                            email 'jsaardchit@bcap.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['maven']
    pkg {
        repo = 'oohlalog'
        userOrg = 'bertramlabs'
        name = 'oohlalog-4j'
        licenses = ['Apache-2.0']
    }
}
